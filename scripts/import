#!/usr/bin/env escript
%%! -pa ../deps/epgsql/ebin ../deps/iso8601/ebin

main([]) ->
    existing_atoms(), % Gotta call it from somewhere to shut up the warning...
    io:put_chars("Usage:\n\timport dir-name\n");
main([Dir]) ->
    {ok, C} = pgsql:connect("localhost", "blerg", "", [{database, "blerg"}]),
    {ok, Files} = file:list_dir(Dir),
    Pred = fun([H|_]) -> H =/= $. end,
    import_files([filename:join(Dir, X) || X <- lists:filter(Pred, Files)], C),
    pgsql:close(C).

import_files([], _C) ->
    ok;
import_files([H|T], C) ->
    import_file(H, C),
    import_files(T, C).

import_file(F, C) ->
    io:format("~s~n", [F]),
    {ok, Bytes} = file:read_file(F),
    Content = binary:split(Bytes, <<"\n">>, [global]),
    Post = parse(Content),
    Title = proplists:get_value(title, Post),
    AuthorId = 1,
    Date = proplists:get_value(date, Post),
    CreatedAt = case Date of
        undefined ->
            filelib:last_modified(F);
        _ ->
            iso8601:parse(Date)
    end,
    Body = proplists:get_value(body, Post),
    pgsql:equery(C, "INSERT INTO posts(title, author_id, created_at, body) VALUES($1, $2, $3, $4)",
                 [Title, AuthorId, CreatedAt, Body]).

parse(Lines) ->
    parse(init, Lines, []).

parse(init, [<<"---">>|Lines], Acc) ->
    parse(topmatter, Lines, Acc);
parse(init, Lines, Acc) ->
    parse(body, Lines, Acc);
parse(topmatter, [<<"---">>|Lines], Acc) ->
    parse(break, Lines, Acc);
parse(topmatter, [H|Lines], Acc) ->
    [Tag|[Value]] = binary:split(H, <<":">>, []),
    Acc2 = [parse_topmatter_tag(Tag, Value)|Acc],
    parse(topmatter, Lines, Acc2);
parse(break, [<<>>|Lines], Acc) ->
    parse(body, Lines, Acc);
parse(break, [Lines], Acc) ->
    parse(body, Lines, Acc);
parse(body, Lines, Acc) ->
    [{body, string:join([binary_to_list(X) || X <- Lines], "\n")}|Acc].

parse_topmatter_tag(<<"title">> = Tag, Value) ->
    TagAtom = list_to_existing_atom(binary_to_list(Tag)),
    ValueTrim = string:strip(string:strip(binary_to_list(Value)), both, $"),
    {TagAtom, ValueTrim};
parse_topmatter_tag(Tag, Value) ->
    TagAtom = list_to_existing_atom(binary_to_list(Tag)),
    ValueTrim = string:strip(binary_to_list(Value)),
    {TagAtom, ValueTrim}.

existing_atoms() ->
    [layout, title, date, tags, alias].
%% -*- erlang -*-
%% vim: ft=erlang

